buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath "$rootProject.fabricClassPath"
    }
}
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

final Properties signingProperties = new Properties()
signingProperties.load(new FileInputStream(file('local/signing.properties')))

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

android {
    compileSdkVersion "$rootProject.compileSdkVersion".toInteger()
    buildToolsVersion "$rootProject.buildToolsVersion"

    defaultConfig {
        applicationId 'io.github.ryanhoo.firFlight'
        minSdkVersion "$rootProject.minSdkVersion".toInteger()
        targetSdkVersion "$rootProject.targetSdkVersion".toInteger()
        versionCode "$VERSION_CODE".toInteger()
        versionName "$VERSION_NAME"
    }

    signingConfigs {
        release {
            storeFile file("local/keystore.jks")
            keyAlias signingProperties['KEY_ALIAS']
            storePassword signingProperties['KEY_STORE_PASSWORD']
            keyPassword signingProperties['KEY_PASSWORD']
        }
    }

    lintOptions {
        disable 'InvalidPackage'
    }

    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }

    buildTypes {
        debug {
            versionNameSuffix '(D)'
        }

        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    productFlavors {
        develop

        fir

        // WanDouJia
        // YingYongBao
        // GooglePlay
    }

    productFlavors.all { flavor ->
        manifestPlaceholders.put('flightFlavorName', flavor.name)
        // Unnecessary, there already has a FLAVOR in BuildConfig
        // flavor.buildConfigField('String', 'FLAVOR_NAME', "\"${flavor.name}\"")
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    testCompile "junit:junit:$rootProject.junitVersion"

    compile "com.android.support:support-v4:$rootProject.supportv4Version"

    compile "com.android.support:appcompat-v7:$rootProject.supportLibraryVersion"
    compile "com.android.support:design:$rootProject.supportLibraryVersion"
    compile "com.android.support:recyclerview-v7:$rootProject.supportLibraryVersion"
    compile "com.android.support:cardview-v7:$rootProject.supportLibraryVersion"
    compile "com.android.support:customtabs:$rootProject.supportLibraryVersion"

    // Image Loader https://github.com/bumptech/glide
    compile "com.github.bumptech.glide:glide:$rootProject.glideVersion"

    compile "com.jakewharton:butterknife:$rootProject.butterKnifeVersion"

    compile "com.google.code.gson:gson:$rootProject.gsonVersion"

    compile "com.squareup.retrofit2:retrofit:$rootProject.retrofitVersion"
    compile "com.squareup.retrofit2:converter-gson:$rootProject.retrofitVersion"

    // BugHD http://bughd.com/doc/android
    compile "im.fir:fir-sdk:$rootProject.bugHDVersion@aar"

    compile("com.crashlytics.sdk.android:crashlytics:$rootProject.crashlyticsVersion@aar") {
        transitive = true;
    }
    compile("com.crashlytics.sdk.android:answers:$rootProject.answersVersion@aar") {
        transitive = true;
    }
}
